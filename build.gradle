/*
 * Copyright (c) 2015-2017 Uli Bubenheimer.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

import net.ltgt.gradle.errorprone.CheckSeverity

buildscript {
    ext.android_plugin_version = '3.6.0-alpha02'
    ext.kotlin_version = '1.3.31'

    repositories {
        mavenLocal()
        google()
        gradlePluginPortal()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$android_plugin_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
        classpath 'net.ltgt.gradle:gradle-errorprone-plugin:0.8'
    }
}


repositories {
    mavenLocal()
    google()
    jcenter()
}

tasks.withType(JavaCompile) {
    afterEvaluate {
        options.errorprone {
            disableWarningsInGeneratedCode = true
            check('OperatorPrecedence', CheckSeverity.OFF)
            excludedPaths = '.*/build/generated/source/.+/databinding/.+\\.java'
        }
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'net.ltgt.errorprone'

group = 'org.bubenheimer'
version = '1.0-SNAPSHOT'

android {
    compileSdkVersion 28

    defaultConfig {
        targetSdkVersion 28
        minSdkVersion 14
        versionCode 1
        versionName '1.0'
        consumerProguardFiles 'proguard-rules.pro'
    }
    buildTypes {
        debug {
        }
        release {
            shrinkResources false
            minifyEnabled false
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
        }
    }
    compileOptions {
      sourceCompatibility JavaVersion.VERSION_1_8
      targetCompatibility JavaVersion.VERSION_1_8
    }

    // TODO change based on resolution of https://issuetracker.google.com/issues/72050365
    libraryVariants.all {
        it.generateBuildConfig.enabled = false
    }

    dataBinding {
        enabled = true
    }
}

dependencies {
    errorprone 'com.google.errorprone:error_prone_core:2.3.3'
    //noinspection GradleDynamicVersion
    errorproneJavac 'com.google.errorprone:javac:9+181-r4173-1'

    String androidxVersion = '1.0.0'

    implementation 'org.bubenheimer:javautil:1.0-SNAPSHOT'

    implementation "androidx.annotation:annotation:$androidxVersion"
    // Give users access to GridLayout attributes, etc.
    api "androidx.gridlayout:gridlayout:$androidxVersion"
    implementation "androidx.preference:preference:$androidxVersion"
    implementation "androidx.recyclerview:recyclerview-selection:$androidxVersion"
    implementation 'androidx.lifecycle:lifecycle-common-java8:2.0.0'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    kapt "androidx.databinding:databinding-compiler:$android_plugin_version"
}
