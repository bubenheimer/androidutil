/*
 * Copyright (c) 2015-2020 Uli Bubenheimer
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

plugins {
    alias libs.plugins.android.library
    alias libs.plugins.kotlin.android
    id("maven-publish")
}

group = 'org.bubenheimer'
version = '1.0-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(libs.versions.java.toolchain.get())
    }
}

android {
    namespace "org.bubenheimer.android.util"

    compileSdk libs.versions.android.sdk.compile.get().toInteger()

    defaultConfig {
        minSdk libs.versions.android.sdk.min.get().toInteger()
        targetSdk libs.versions.android.sdk.target.get().toInteger()
        versionCode 1
        versionName version
        consumerProguardFiles 'proguard-rules.pro'
    }

    buildTypes {
        debug {
        }
        release {
            shrinkResources false
            minifyEnabled false
            debuggable false
            jniDebuggable false
        }
    }

    compileOptions {
        sourceCompatibility libs.versions.java.source.get()
        targetCompatibility libs.versions.java.source.get()
    }

    kotlinOptions {
        jvmTarget = libs.versions.java.source.get()
        freeCompilerArgs = [
                "-progressive",
                "-Xexplicit-api=strict",
                "-Xjvm-default=all"
        ]
    }

    buildFeatures {
        buildConfig false
    }

    publishing {
        multipleVariants {
            allVariants()
            withSourcesJar()
        }
    }

    lint {
        quiet false
        abortOnError false
        checkReleaseBuilds true
        ignoreWarnings false
        absolutePaths true
        checkAllWarnings true
        warningsAsErrors true
        // if true, don't include source code lines in the error output
        noLines false
        // if true, show all locations for an error, do not truncate lists, etc.
        showAll true
        // whether lint should include full issue explanations in the text error output
        explainIssues true
        textReport false
        xmlReport false
        htmlReport true
        // optional path to HTML report (default will be lint-results.html in the builddir)
        //htmlOutput file("$reportsDir/lint-report.html")
        checkTestSources true
        ignoreTestSources false
        checkGeneratedSources false
        checkDependencies false
    }

    dependencies {
        implementation libs.bubenheimer.kotlinutil

        implementation libs.androidx.annotation
        implementation libs.androidx.core.ktx

        testImplementation libs.junit

        testImplementation libs.kotlin.test.junit
    }
}

publishing {
    publications {
        library(MavenPublication) {
            afterEvaluate {
                from components.default
            }
        }
    }
}
