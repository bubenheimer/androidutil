/*
 * Copyright (c) 2015-2020 Uli Bubenheimer
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

buildscript {
    ext.android_plugin_version = '7.0.0'
    ext.kotlin_version = '1.5.21'

    repositories {
        mavenLocal()
        google()
        gradlePluginPortal()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$android_plugin_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

repositories {
    mavenLocal()
    google()
    mavenCentral()
}

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'kotlin-android'

group = 'org.bubenheimer'
version = '1.0-SNAPSHOT'

final String javaVersion = "1.8"

android {
    compileSdkVersion 30

    defaultConfig {
        targetSdkVersion 30
        minSdkVersion 19
        versionCode 1
        versionName '1.0'
        consumerProguardFiles 'proguard-rules.pro'
    }

    buildTypes {
        debug {
        }
        release {
            shrinkResources false
            minifyEnabled false
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
        }
    }

    compileOptions {
        sourceCompatibility javaVersion
        targetCompatibility javaVersion
    }

    kotlinOptions {
        jvmTarget = javaVersion
        freeCompilerArgs = [
                "-progressive",
                "-Xexplicit-api=strict",
                "-Xjvm-default=all",
                "-Xopt-in=kotlin.contracts.ExperimentalContracts"
        ]
    }

    buildFeatures {
        buildConfig false
        viewBinding true
    }

    lintOptions {
        quiet false
        abortOnError false
        checkReleaseBuilds true
        ignoreWarnings false
        absolutePaths true
        checkAllWarnings true
        warningsAsErrors true
        // if true, don't include source code lines in the error output
        noLines false
        // if true, show all locations for an error, do not truncate lists, etc.
        showAll true
        // whether lint should include full issue explanations in the text error output
        explainIssues true
        textReport false
        xmlReport false
        htmlReport true
        // optional path to HTML report (default will be lint-results.html in the builddir)
        //htmlOutput file("$reportsDir/lint-report.html")
        checkTestSources true
        ignoreTestSources false
        checkGeneratedSources false
        checkDependencies false
    }

    dependencies {
        implementation 'org.bubenheimer:kotlinutil:1.0-SNAPSHOT'

        implementation "androidx.annotation:annotation:1.2.0"
        implementation "androidx.core:core-ktx:1.6.0"
        implementation "androidx.fragment:fragment-ktx:1.3.6"
        // using api to give users access to GridLayout attributes, etc.
        api "androidx.gridlayout:gridlayout:1.0.0"
        implementation "androidx.preference:preference-ktx:1.1.1"
        implementation "androidx.recyclerview:recyclerview-selection:1.1.0"
        implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.3.1'
        implementation 'androidx.lifecycle:lifecycle-common-java8:2.3.1'

        testImplementation("junit:junit:4.13.2")

        testImplementation("org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version")
    }

    task debugSourceJar(type: Jar) {
        sourceSets {
            from debug.java.srcDirs, main.java.srcDirs
        }
        //noinspection GroovyAccessibility
        archiveClassifier = "sources"
    }
}

publishing {
    publications {
        debug(MavenPublication) {
            afterEvaluate {
                from components.debug
                artifactId = "$project.name-debug"

                artifact(debugSourceJar)
            }
        }

        release(MavenPublication) {
            afterEvaluate {
                from components.release
                artifactId = "$project.name-release"
            }
        }
    }
}
